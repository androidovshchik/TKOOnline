apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'AndResGuard'

android {
    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 26
        //noinspection ExpiredTargetSdkVersion // баг с запросом телефона по умолчанию
        targetSdkVersion 28
        versionCode 33
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        kapt {
            arguments {
                arg('room.schemaLocation', "$projectDir/schemas")
                arg('room.incremental', 'true')
            }
        }
    }
    signingConfigs {
        release {
            storeFile file('keystore/release.jks')
            storePassword 'tkonline'
            keyAlias 'tkonline'
            keyPassword 'tkonline'
            v1SigningEnabled true
            v2SigningEnabled true
            // countryCode 07
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'default'
    productFlavors {
        dev {
            buildConfigField('boolean', 'PROD', 'false')
        }
        prod {
            buildConfigField('boolean', 'PROD', 'true')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "tkoonline-${variant.productFlavors[0].name}-${variant.buildType.name}${variant.versionCode}.apk"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

// ./gradlew resguard[BuildType | Flavor]
andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    // it will keep the origin path of your resources when it's true
    keepRoot = false
    whiteList = []
    compressFilePattern = [
        '*.png',
        '*.jpg',
        '*.jpeg',
        '*.gif'
    ]
    sevenzip {
        artifact = "com.tencent.mm:SevenZip:$andresguard_version"
        //path = '/usr/local/bin/7za'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk23:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk23-listeners:$anko_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.0-beta01'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.camera:camera-camera2:1.1.0-alpha08'
    implementation 'androidx.camera:camera-lifecycle:1.1.0-alpha08'
    implementation 'androidx.camera:camera-view:1.0.0-alpha28'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation "org.kodein.di:kodein-di-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-core:$kodein_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation platform('com.squareup.okhttp3:okhttp-bom:4.9.1')
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:logging-interceptor'
    implementation 'com.rabbitmq:amqp-client:5.13.1'
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation 'com.jsibbold:zoomage:1.3.1'
    implementation 'com.chibatching.kotpref:kotpref:2.13.1'
    implementation 'io.coil-kt:coil:1.3.2'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.elvishew:xlog:1.11.0'
    implementation 'org.apache.commons:commons-text:1.9'
    devImplementation "com.facebook.stetho:stetho:$stetho_version"
    devImplementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
