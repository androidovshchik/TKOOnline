apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'AndResGuard'

android {
    //noinspection GradleCompatible
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 7
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("keystore/release.jks")
            storePassword "tkonline"
            keyAlias "tkonline"
            keyPassword "tkonline"
            v1SigningEnabled true
            v2SigningEnabled true
            // countryCode 07
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            //buildConfigField("String", "BASE_URL", "\"https://msknt2.iqsolution.ru/mobile/v1/\"")
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //buildConfigField("String", "BASE_URL", "\"https://msknt.iqsolution.ru/mobile/v1/\"")
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${applicationId.substring(applicationId.lastIndexOf(".") + 1)}${variant.versionCode}-${variant.buildType.name}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        //checkReleaseBuilds false
        //abortOnError false
    }
}

// ./gradlew resguard[BuildType | Flavor]
andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    // it will keep the origin path of your resources when it's true
    keepRoot = false
    whiteList = []
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif"
    ]
    sevenzip {
        artifact = "com.tencent.mm:SevenZip:${Versions.andresguard}"
        //path = "/usr/local/bin/7za"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${Versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1"
    implementation "org.jetbrains.anko:anko-commons:${Versions.anko}"
    implementation "org.jetbrains.anko:anko-sdk23:${Versions.anko}"
    implementation "org.jetbrains.anko:anko-sdk23-listeners:${Versions.anko}"
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta04'
    implementation 'androidx.exifinterface:exifinterface:1.1.0-beta01'
    implementation 'androidx.work:work-runtime-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-livedata:${Versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${Versions.lifecycle}"
    implementation "androidx.room:room-runtime:${Versions.room}"
    kapt "androidx.room:room-compiler:${Versions.room}"
    implementation 'com.google.android.gms:play-services-vision:18.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation "org.kodein.di:kodein-di-generic-jvm:${Versions.kodein}"
    implementation "org.kodein.di:kodein-di-framework-android-core:${Versions.kodein}"
    implementation "com.squareup.retrofit2:retrofit:${Versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${Versions.retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'
    implementation 'com.rabbitmq:amqp-client:5.7.3'
    implementation 'net.danlew:android.joda:2.10.3'
    implementation 'com.chibatching.kotpref:kotpref:2.9.1'
    implementation 'io.coil-kt:coil:0.7.0'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.facebook.stetho:stetho:${Versions.stetho}"
    implementation "com.facebook.stetho:stetho-okhttp3:${Versions.stetho}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion Versions.kotlin
        }
    }
}
